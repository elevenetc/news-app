apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.elevenetc.android.news"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        all {
            final newsApiOrgConfig = getProperties('news.api.org.config')
            final newsApiOrgApiKey = newsApiOrgConfig.getProperty('api.key')

            buildConfigField 'String', 'NEWSAPIORG_API_KEY', "\"$newsApiOrgApiKey\""
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

}

dependencies {

    final verDagger = '2.16'
    final verRetrofit = '2.6.0'
    final verPicasso = '2.71828'
    final verRecyclerView = '1.1.0'
    final verRxJava = '2.1.14'
    final verRxAndroid = '2.1.1'
    final verLifecycle = '2.2.0'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    /**
     * Common
     */
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.4.0-alpha01'

    /**
     * Architecture
     */
    implementation "com.google.dagger:dagger:$verDagger"
    kapt "com.google.dagger:dagger-compiler:$verDagger"

    /**
     * Network
     */
    implementation "com.squareup.retrofit2:retrofit:$verRetrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$verRetrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$verRetrofit"
    implementation "com.squareup.picasso:picasso:$verPicasso"

    /**
     * UI
     */
    implementation "androidx.recyclerview:recyclerview:$verRecyclerView"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'

    /**
     * Testing
     */
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.assertj:assertj-core:3.15.0'
    androidTestImplementation 'junit:junit:4.13'
    androidTestImplementation 'org.assertj:assertj-core:3.15.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"

    /**
     * Utils
     */
    implementation "io.reactivex.rxjava2:rxjava:$verRxJava"
    implementation "io.reactivex.rxjava2:rxandroid:$verRxAndroid"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$verLifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$verLifecycle"
}

def getProperties(name) {

    if (!project.rootProject.file(name).exists()) {
        def errorMessage = name + " file is not found. Read more at https://github.com/elevenetc/news-app"
        throw new FileNotFoundException(errorMessage)
    }

    Properties properties = new Properties()
    properties.load(project.rootProject.file(name).newDataInputStream())
    return properties
}